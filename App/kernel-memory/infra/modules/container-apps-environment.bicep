targetScope = 'resourceGroup'

@description('Required. Suffix to create unique resource names; 4-15 characters.')
@minLength(4)
@maxLength(15)
param suffix string = uniqueString(resourceGroup().id)

@description('Required. The location where the resources will be created.')
param location string = resourceGroup().location

@description('Optional. The tags to be assigned to the created resources.')
param tags object = {}

@description('Required. The name of the container apps environment. If set, it overrides the name generated by the template.')
param containerAppsEnvironmentName string = 'km-cae-${suffix}'

@description('Required. The name of the log analytics workspace. If set, it overrides the name generated by the template.')
param logAnalyticsWorkspaceName string = 'km-log-${suffix}'

@description('Required. The name of the application insights. If set, it overrides the name generated by the template.')
param applicationInsightsName string = 'km-appins-${suffix}'

resource logAnalyticsWorkspace 'Microsoft.OperationalInsights/workspaces@2021-06-01' = {
  name: logAnalyticsWorkspaceName
  location: location
  tags: tags
  properties: any({
    features: {
      searchVersion: 1
    }
    sku: {
      name: 'PerGB2018'
    }
    retentionInDays: 30
  })
}

resource applicationInsights 'Microsoft.Insights/components@2020-02-02' = {
  name: applicationInsightsName
  location: location
  tags: tags
  kind: 'web'
  properties: {
    Application_Type: 'web'
    WorkspaceResourceId: logAnalyticsWorkspace.id
  }
}

resource containerAppsEnvironment 'Microsoft.App/managedEnvironments@2022-10-01' = {
  name: containerAppsEnvironmentName
  location: location
  tags: tags
  sku: {
    name: 'Consumption'
  }
  properties: {
    daprAIInstrumentationKey: applicationInsights.properties.InstrumentationKey
    appLogsConfiguration: {
      destination: 'log-analytics'
      logAnalyticsConfiguration: {
        customerId: logAnalyticsWorkspace.properties.customerId
        sharedKey: logAnalyticsWorkspace.listKeys().primarySharedKey
      }
    }
  }
}

@description('Contains App Environment Name.')
output containerAppsEnvironmentName string = containerAppsEnvironment.name

@description('Contains App Environment ID.')
output containerAppsEnvironmentId string = containerAppsEnvironment.id

@description('Contains LogAnalytics Workspace Name.')
output logAnalyticsWorkspaceName string = logAnalyticsWorkspace.name

@description('Contains App Insights Name.')
output applicationInsightsName string = applicationInsights.name

@description('Contains App Insights Instrumentation Key.')
output applicationInsightsInstrumentationKey string = applicationInsights.properties.InstrumentationKey

@description('Contains App Insights Connection String.')
output applicationInsightsConnectionString string = applicationInsights.properties.ConnectionString
